# Deadlock freedom

This module proves two deadlock freedom results.

## Imports

```agda
open import Data.Sum
open import Data.Product using (Î£; _Ã—_; _,_; âˆƒ; Î£-syntax; âˆƒ-syntax)
open import Data.Bool using (Bool; if_then_else_)
open Bool using (true; false)

open import Relation.Nullary using (Â¬_; contradiction)
import Relation.Binary.PropositionalEquality as Eq
open Eq using (refl)

open import Type
open import Context
open import Process
open import Reduction
open import Congruence
```

## Additional process classification

We introduce further classes of processes, distinguishing between
**threads** (sequential processes) and **cuts** (parallel
compositions of processes). By ``sequential'' process we mean a
process that begins with an action, or that it is made of only one
action (like `close` and `fail`) or that is a link.

```agda
data Thread : âˆ€{Î“} -> Process Î“ -> Set where
  link :
    âˆ€{Î“ A B}
    (d : Dual A B) (p : Î“ â‰ƒ [ A ] + [ B ]) -> Thread (link d p)
  fail :
    âˆ€{Î“ Î”}
    (p : Î“ â‰ƒ [ âŠ¤ ] + Î”) -> Thread (fail p)
  wait :
    âˆ€{Î“ Î”} (p : Î“ â‰ƒ [ âŠ¥ ] + Î”) {P : Process Î”} -> Thread (wait p P)
  case :
    âˆ€{Î“ Î” A B} (p : Î“ â‰ƒ [ A & B ] + Î”) {P : Process (A :: Î”)} {Q : Process (B :: Î”)} ->
    Thread (case p P Q)
  join :
    âˆ€{Î“ Î” A B} (p : Î“ â‰ƒ [ A â…‹ B ] + Î”) {P : Process (B :: A :: Î”)} ->
    Thread (join p P)
  close : Thread close
  select :
    âˆ€{Î“ Î” A B} (x : Bool) (p : Î“ â‰ƒ [ A âŠ• B ] + Î”) {P : Process ((if x then A else B) :: Î”)} ->
    Thread (select x p P)
  fork :
    âˆ€{Î“ Î” Î”â‚ Î”â‚‚ A B} (p : Î“ â‰ƒ [ A âŠ— B ] + Î”) (q : Î” â‰ƒ Î”â‚ + Î”â‚‚)
    {P : Process (A :: Î”â‚)} {Q : Process (B :: Î”â‚‚)} ->
    Thread (fork p q P Q)

data Cut {Î“} : Process Î“ -> Set where
  cut :
    âˆ€{Î“â‚ Î“â‚‚ A B} (d : Dual A B) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚)
    {P : Process (A :: Î“â‚)} {Q : Process (B :: Î“â‚‚)} ->
    Cut (cut d p P Q)
```

Every process is either a thread or a cut.

```agda
process-is : âˆ€{Î“} (P : Process Î“) -> Thread P âŠŽ Cut P
process-is close = injâ‚ close
process-is (link d p) = injâ‚ (link d p)
process-is (fail p) = injâ‚ (fail p)
process-is (wait p P) = injâ‚ (wait p)
process-is (select x p P) = injâ‚ (select x p)
process-is (case p P Q) = injâ‚ (case p)
process-is (fork p q P Q) = injâ‚ (fork p q)
process-is (join p P) = injâ‚ (join p)
process-is (cut d p P Q) = injâ‚‚ (cut d p)
```

## Thread classification

Concerning threads, we have already made a distinction between
`Input` and `Output` threads. Here we further distinguish links and
**delayed** threads, namely those threads beginning with an action
on a channel different from the youngest one.

```agda
data Link {Î“} : Process Î“ -> Set where
  link :
    âˆ€{A B} (d : Dual A B) (p : Î“ â‰ƒ [ A ] + [ B ]) -> Link (link d p)

data Delayed : âˆ€{Î“} -> Process Î“ -> Set where
  fail :
    âˆ€{A Î“ Î”}
    (p : Î“ â‰ƒ [ âŠ¤ ] + Î”) -> Delayed (fail (split-r {A} p))
  wait :
    âˆ€{C Î“ Î”} (p : Î“ â‰ƒ [ âŠ¥ ] + Î”) {P : Process (C :: Î”)} -> Delayed (wait (split-r p) P)
  case :
    âˆ€{Î“ Î” C A B} (p : Î“ â‰ƒ [ A & B ] + Î”) {P : Process (A :: C :: Î”)} {Q : Process (B :: C :: Î”)} ->
    Delayed (case (split-r p) P Q)
  join :
    âˆ€{Î“ Î” C A B} (p : Î“ â‰ƒ [ A â…‹ B ] + Î”) {P : Process (B :: A :: C :: Î”)} ->
    Delayed (join (split-r p) P)
  select :
    âˆ€{Î“ Î” C A B} (x : Bool) (p : Î“ â‰ƒ [ A âŠ• B ] + Î”) {P : Process ((if x then A else B) :: C :: Î”)} ->
    Delayed (select x (split-r p) P)
  fork-l :
    âˆ€{Î“ Î” Î”â‚ Î”â‚‚ C A B} (p : Î“ â‰ƒ [ A âŠ— B ] + Î”) (q : Î” â‰ƒ Î”â‚ + Î”â‚‚)
    {P : Process (A :: C :: Î”â‚)} {Q : Process (B :: Î”â‚‚)} ->
    Delayed (fork (split-r p) (split-l q) P Q)
  fork-r :
    âˆ€{Î“ Î” Î”â‚ Î”â‚‚ C A B} (p : Î“ â‰ƒ [ A âŠ— B ] + Î”) (q : Î” â‰ƒ Î”â‚ + Î”â‚‚)
    {P : Process (A :: Î”â‚)} {Q : Process (B :: C :: Î”â‚‚)} ->
    Delayed (fork (split-r p) (split-r q) P Q)
```

Every thread is either a link, a delayed thread, an input or an
output.

```agda
thread-is : âˆ€{Î“} {P : Process Î“} -> Thread P ->
  Link P âŠŽ Delayed P âŠŽ Input P âŠŽ Output P
thread-is (link d p) = injâ‚ (link d p)
thread-is (fail (split-l p)) = injâ‚‚ (injâ‚‚ (injâ‚ (fail p)))
thread-is (fail (split-r p)) = injâ‚‚ (injâ‚ (fail p))
thread-is (wait (split-l p)) = injâ‚‚ (injâ‚‚ (injâ‚ (wait p)))
thread-is (wait (split-r p)) = injâ‚‚ (injâ‚ (wait p))
thread-is (case (split-l p)) = injâ‚‚ (injâ‚‚ (injâ‚ (case p)))
thread-is (case (split-r p)) = injâ‚‚ (injâ‚ (case p))
thread-is (join (split-l p)) = injâ‚‚ (injâ‚‚ (injâ‚ (join p)))
thread-is (join (split-r p)) = injâ‚‚ (injâ‚ (join p))
thread-is close = injâ‚‚ (injâ‚‚ (injâ‚‚ close))
thread-is (select x (split-l p)) = injâ‚‚ (injâ‚‚ (injâ‚‚ (select x p)))
thread-is (select x (split-r p)) = injâ‚‚ (injâ‚ (select x p))
thread-is (fork (split-l p) q) = injâ‚‚ (injâ‚‚ (injâ‚‚ (fork p q)))
thread-is (fork (split-r p) (split-l q)) = injâ‚‚ (injâ‚ (fork-l p q))
thread-is (fork (split-r p) (split-r q)) = injâ‚‚ (injâ‚ (fork-r p q))
```

## Canonical cuts

Structural precongruence and reduction operate on cuts having a
particular form, which we call **canonical**.

```agda
data CanonicalCut {Î“} : Process Î“ -> Set where
  cc-link :
    âˆ€{Î“â‚ Î“â‚‚ A B} (d : Dual A B) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚)
    {P : Process (A :: Î“â‚)} {Q : Process (B :: Î“â‚‚)} ->
    Link P -> CanonicalCut (cut d p P Q)
  cc-delayed :
    âˆ€{Î“â‚ Î“â‚‚ A B} (d : Dual A B) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚)
    {P : Process (A :: Î“â‚)} {Q : Process (B :: Î“â‚‚)} ->
    Delayed P -> CanonicalCut (cut d p P Q)
  cc-redex :
    âˆ€{Î“â‚ Î“â‚‚ A B} (d : Dual A B) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚)
    {P : Process (A :: Î“â‚)} {Q : Process (B :: Î“â‚‚)} ->
    Output P -> Input Q -> CanonicalCut (cut d p P Q)
```

Every cut between two threads is structurally precongruent to a
canonical cut.

```agda
canonical-cut :
  âˆ€{Î“ Î“â‚ Î“â‚‚ A B}
  {P : Process (A :: Î“â‚)} {Q : Process (B :: Î“â‚‚)}
  (d : Dual A B) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚) ->
  Thread P -> Thread Q ->
  âˆƒ[ R ] CanonicalCut R Ã— cut d p P Q âŠ’ R
canonical-cut dc pc Pt Qt with thread-is Pt | thread-is Qt
... | injâ‚ x | y = _ , cc-link dc pc x , s-refl
... | injâ‚‚ (injâ‚ x) | y = _ , cc-delayed dc pc x , s-refl
... | injâ‚‚ (injâ‚‚ x) | injâ‚ y = _ , cc-link (dual-symm dc) (+-comm pc) y , s-comm dc (dual-symm dc) pc (+-comm pc)
... | injâ‚‚ (injâ‚‚ x) | injâ‚‚ (injâ‚ y) = _ , cc-delayed (dual-symm dc) (+-comm pc) y , s-comm dc (dual-symm dc) pc (+-comm pc)
... | injâ‚‚ (injâ‚‚ (injâ‚ x)) | injâ‚‚ (injâ‚‚ (injâ‚ y)) = contradiction (x , y) (input-input dc)
... | injâ‚‚ (injâ‚‚ (injâ‚ x)) | injâ‚‚ (injâ‚‚ (injâ‚‚ y)) = _ , cc-redex (dual-symm dc) (+-comm pc) y x , s-comm dc (dual-symm dc) pc (+-comm pc)
... | injâ‚‚ (injâ‚‚ (injâ‚‚ x)) | injâ‚‚ (injâ‚‚ (injâ‚ y)) = _ , cc-redex dc pc x y , s-refl
... | injâ‚‚ (injâ‚‚ (injâ‚‚ x)) | injâ‚‚ (injâ‚‚ (injâ‚‚ y)) = contradiction (x , y) (output-output dc)
```

## Deadlock freedom for general processes

We say that a process is **observable** if it is (structurally
precongruent to) a thread. The terminology is justified by the fact
that a thread necessarily performs an action on a free channel so
this action can be ``observed'' if the thread is composed in
parallel with another process.

```agda
Observable : âˆ€{Î“} -> Process Î“ -> Set
Observable P = âˆƒ[ Q ] P âŠ’ Q Ã— Thread Q
```

Then, we say that a process is **live** if it is either observable
or reducible. **Deadlock freedom** is then defined as the
preservation of liveness throughout reductions.

```agda
Live : âˆ€{Î“} -> Process Î“ -> Set
Live P = Observable P âŠŽ Reducible P

DeadlockFree : âˆ€{Î“} -> Process Î“ -> Set
DeadlockFree {Î“} P = âˆ€(Q : Process Î“) -> P => Q -> Live Q
```

A few auxiliary results about the `Live` predicate follow. First of
all, we prove that `Live` is backward preserved by structural
precongruence.

```agda
âŠ’Live : âˆ€{Î“} {P Q : Process Î“} -> P âŠ’ Q -> Live Q -> Live P
âŠ’Live pcong (injâ‚ (_ , x , th)) = injâ‚ (_ , s-tran pcong x , th)
âŠ’Live pcong (injâ‚‚ (_ , red)) = injâ‚‚ (_ , r-cong pcong red)
```

Also, every (well-typed) process is `Live`.

```agda
live-cut : âˆ€{Î“} {P : Process Î“} -> CanonicalCut P -> Live P
live-cut (cc-link d p (link e (split-l (split-r split-e)))) with dual-fun-r e d
... | refl = injâ‚‚ (_ , r-link d e p)
live-cut (cc-link d p (link e (split-r (split-l split-e)))) with dual-fun-l e (dual-symm d)
... | refl = injâ‚‚ (_ , r-cong (s-cong-l d p (s-link e (split-r (split-l split-e)))) (r-link d (dual-symm e) p))
live-cut (cc-redex d-ðŸ™-âŠ¥ p close (wait q)) with +-empty-l q | +-empty-l p
... | refl | refl = injâ‚‚ (_ , r-close p q)
live-cut (cc-redex (d-âŠ•-& d e) p (select false q) (case r)) with +-empty-l q | +-empty-l r
... | refl | refl = injâ‚‚ (_ , r-select-r d e p q r)
live-cut (cc-redex (d-âŠ•-& d e) p (select true q) (case r)) with +-empty-l q | +-empty-l r
... | refl | refl = injâ‚‚ (_ , r-select-l d e p q r)
live-cut (cc-redex (d-âŠ—-â…‹ d e) p (fork q r) (join s)) with +-empty-l q | +-empty-l s
... | refl | refl = injâ‚‚ (_ , r-fork d e p s r q)
live-cut (cc-delayed d p (fail q)) =
  let _ , _ , q' = +-assoc-l p q in
  injâ‚ (_ , s-fail d p q , fail q')
live-cut (cc-delayed d p (wait q)) =
  let _ , _ , q' = +-assoc-l p q in
  injâ‚ (_ , s-wait d p q , wait q')
live-cut (cc-delayed d p (case q)) =
  let _ , _ , q' = +-assoc-l p q in
  injâ‚ (_ , s-case d p q , case q')
live-cut (cc-delayed d p (join q)) =
  let _ , _ , q' = +-assoc-l p q in
  injâ‚ (_ , s-join d p q , join q')
live-cut (cc-delayed d p (select false q)) =
  let _ , _ , q' = +-assoc-l p q in
  injâ‚ (_ , s-select-r d p q , select false q')
live-cut (cc-delayed d p (select true q)) =
  let _ , _ , q' = +-assoc-l p q in
  injâ‚ (_ , s-select-l d p q , select true q')
live-cut (cc-delayed d p (fork-l q r)) =
  let _ , p' , q' = +-assoc-l p q in
  let _ , p'' , r' = +-assoc-l p' r in
  let _ , q'' , r'' = +-assoc-r r' (+-comm p'') in
  injâ‚ (_ , s-fork-l d p q r , fork q' r'')
live-cut (cc-delayed d p (fork-r q r)) =
  let _ , p' , q' = +-assoc-l p q in
  let _ , p'' , r' = +-assoc-l p' r in
  injâ‚ (_ , s-fork-r d p q r , fork q' r')

live : âˆ€{Î“} (P : Process Î“) -> Live P
live P with process-is P
... | injâ‚ x = injâ‚ (_ , s-refl , x)
... | injâ‚‚ (cut d p {P} {Q}) with live P
... | injâ‚‚ (P' , red) = injâ‚‚ (_ , r-cut d p red)
... | injâ‚ (P' , Pc , Pt) with live Q
... | injâ‚‚ (Q' , red) = injâ‚‚ (_ , r-cong (s-comm d (dual-symm d) p (+-comm p)) (r-cut (dual-symm d) (+-comm p) red))
... | injâ‚ (Q' , Qc , Qt) with canonical-cut d p Pt Qt
... | _ , cc , pcong = âŠ’Live (s-tran (s-cong-2 d p Pc Qc) pcong) (live-cut cc)
```

At this point the proof of deadlock freedom is straightforward.

```agda
deadlock-freedom : âˆ€{Î“} (P : Process Î“) -> DeadlockFree P
deadlock-freedom P Q reds = live Q
```

## Deadlock freedom for closed processes

Processes that are well typed in a singleton context containing only
the type `One` enjoy a specialized deadlock-freedom result that more
closely resembles those for calculi/languages not based on logic. To
prove this version of deadlock freedom, let us introduce the `Close`
class to easily identify `close p` processes.

```agda
data Close : âˆ€{Î“} -> Process Î“ -> Set where
  close : Close close
```

It is easy to prove that the only thread that is well typed in the
singleton context `[ One ]` is `Close`.

```agda
thread-closed : {P : Process [ ðŸ™ ]} -> Thread P -> Close P
thread-closed (link d (split-l ()))
thread-closed (link d (split-r ()))
thread-closed (fail (split-r ()))
thread-closed (wait (split-r ()))
thread-closed (case (split-r ()))
thread-closed (join (split-r ()))
thread-closed close = close
thread-closed (select x (split-r ()))
thread-closed (fork (split-r ()) q)
```

Further, `Close` is backward preserved by structural precongruence.

```agda
âŠ’Close : {P Q : Process [ ðŸ™ ]} -> P âŠ’ Q -> Close Q -> Close P
âŠ’Close s-refl Qc = Qc
âŠ’Close (s-tran pcongâ‚ pcongâ‚‚) Qc = âŠ’Close pcongâ‚ (âŠ’Close pcongâ‚‚ Qc)
```

The specialized version of deadlock freedom that we prove is based
on `Live'` predicate that characterizes those processes that are
either `Close` or `Reducible`.

```agda
Live' : âˆ€{Î“} -> Process Î“ -> Set
Live' P = Close P âŠŽ Reducible P

DeadlockFree' : âˆ€{Î“} -> Process Î“ -> Set
DeadlockFree' {Î“} P = âˆ€(Q : Process Î“) -> P => Q -> Live' Q
```

Every process that is well typed in the singleton context `[ ðŸ™ ]`
is also `Live'` and therefore `DeadlockFree'`.

```agda
live' : (P : Process [ ðŸ™ ]) -> Live' P
live' P with live P
... | injâ‚‚ x = injâ‚‚ x
... | injâ‚ (Q , pcong , Qt) = injâ‚ (âŠ’Close pcong (thread-closed Qt))

deadlock-freedom' : (P : Process [ ðŸ™ ]) -> DeadlockFree' P
deadlock-freedom' P Q reds = live' Q
```
