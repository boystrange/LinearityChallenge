# Subtyping

This module defines subtyping and proves its soundness.

## Imports

```agda
open import Data.Bool using (Bool; if_then_else_)
open Bool using (true; false)
open import Data.Sum
open import Data.Product using (Î£; _Ã—_; _,_; âˆƒ; Î£-syntax; âˆƒ-syntax)
open import Relation.Nullary using (Â¬_; contradiction)
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; refl; congâ‚‚)

open import Type
open import Context
open import Process
open import LinkElimination
```

## Definition

We start by defining the subtyping relation `<=` for (finite)
session types as described in the paper by [Horne and
Padovani](http://dx.doi.org/10.1016/j.jlamp.2024.100986). Basically,
ğŸ˜ is the least session type and âŠ¤ is the greatest one. Every other
relation follows from the expected properties of `<=`: it should be
reflexive on constants and *covariant* with respect to every
connective.

```agda
infix 4 _<=_

data _<=_ : Type -> Type -> Set where
  sub-0 : âˆ€{A} -> ğŸ˜ <= A
  sub-âŠ¤ : âˆ€{A} -> A <= âŠ¤
  sub-1 : ğŸ™ <= ğŸ™
  sub-âŠ¥ : âŠ¥ <= âŠ¥
  sub-& : âˆ€{A B A' B'} -> A <= A' -> B <= B' -> A & B <= A' & B'
  sub-âŠ• : âˆ€{A B A' B'} -> A <= A' -> B <= B' -> A âŠ• B <= A' âŠ• B'
  sub-â…‹ : âˆ€{A B A' B'} -> A <= A' -> B <= B' -> A â…‹ B <= A' â…‹ B'
  sub-âŠ— : âˆ€{A B A' B'} -> A <= A' -> B <= B' -> A âŠ— B <= A' âŠ— B'
```

## Properties

The fact that `<=` is reflexive in general, transitive and
antisymmetric is proved below.

```agda
<=-refl : âˆ€{A} -> A <= A
<=-refl {ğŸ˜} = sub-0
<=-refl {ğŸ™} = sub-1
<=-refl {âŠ¥} = sub-âŠ¥
<=-refl {âŠ¤} = sub-âŠ¤
<=-refl {A & B} = sub-& <=-refl <=-refl
<=-refl {A âŠ• B} = sub-âŠ• <=-refl <=-refl
<=-refl {A âŠ— B} = sub-âŠ— <=-refl <=-refl
<=-refl {A â…‹ B} = sub-â…‹ <=-refl <=-refl

<=-tran : âˆ€{A B C} -> A <= B -> B <= C -> A <= C
<=-tran sub-0 t = sub-0
<=-tran s sub-âŠ¤ = sub-âŠ¤
<=-tran sub-1 t = t
<=-tran sub-âŠ¥ t = t
<=-tran (sub-& sâ‚ sâ‚‚) (sub-& tâ‚ tâ‚‚) = sub-& (<=-tran sâ‚ tâ‚) (<=-tran sâ‚‚ tâ‚‚)
<=-tran (sub-âŠ• sâ‚ sâ‚‚) (sub-âŠ• tâ‚ tâ‚‚) = sub-âŠ• (<=-tran sâ‚ tâ‚) (<=-tran sâ‚‚ tâ‚‚)
<=-tran (sub-â…‹ sâ‚ sâ‚‚) (sub-â…‹ tâ‚ tâ‚‚) = sub-â…‹ (<=-tran sâ‚ tâ‚) (<=-tran sâ‚‚ tâ‚‚)
<=-tran (sub-âŠ— sâ‚ sâ‚‚) (sub-âŠ— tâ‚ tâ‚‚) = sub-âŠ— (<=-tran sâ‚ tâ‚) (<=-tran sâ‚‚ tâ‚‚)

<=-anti-symm : âˆ€{A B} -> A <= B -> B <= A -> A â‰¡ B
<=-anti-symm sub-0 sub-0 = refl
<=-anti-symm sub-âŠ¤ sub-âŠ¤ = refl
<=-anti-symm sub-1 sub-1 = refl
<=-anti-symm sub-âŠ¥ sub-âŠ¥ = refl
<=-anti-symm (sub-& sâ‚ sâ‚‚) (sub-& tâ‚ tâ‚‚) = congâ‚‚ _&_ (<=-anti-symm sâ‚ tâ‚) (<=-anti-symm sâ‚‚ tâ‚‚)
<=-anti-symm (sub-âŠ• sâ‚ sâ‚‚) (sub-âŠ• tâ‚ tâ‚‚) = congâ‚‚ _âŠ•_ (<=-anti-symm sâ‚ tâ‚) (<=-anti-symm sâ‚‚ tâ‚‚)
<=-anti-symm (sub-â…‹ sâ‚ sâ‚‚) (sub-â…‹ tâ‚ tâ‚‚) = congâ‚‚ _â…‹_ (<=-anti-symm sâ‚ tâ‚) (<=-anti-symm sâ‚‚ tâ‚‚)
<=-anti-symm (sub-âŠ— sâ‚ sâ‚‚) (sub-âŠ— tâ‚ tâ‚‚) = congâ‚‚ _âŠ—_ (<=-anti-symm sâ‚ tâ‚) (<=-anti-symm sâ‚‚ tâ‚‚)
```

Notoriously, `<=` should behave contravariantly with respect to
duality, namely if $A \leq B$ then we expect $B^\bot \leq
A^\bot$. This is proved below.

```agda
dual<= : âˆ€{A A' B B'} -> Dual A A' -> Dual B B' -> A <= B -> B' <= A'
dual<= d-ğŸ˜-âŠ¤ e sub-0 = sub-âŠ¤
dual<= d d-âŠ¤-ğŸ˜ sub-âŠ¤ = sub-0
dual<= d-ğŸ™-âŠ¥ d-ğŸ™-âŠ¥ sub-1 = sub-âŠ¥
dual<= d-âŠ¥-ğŸ™ d-âŠ¥-ğŸ™ sub-âŠ¥ = sub-1
dual<= (d-&-âŠ• dâ‚ dâ‚‚) (d-&-âŠ• eâ‚ eâ‚‚) (sub-& sâ‚ sâ‚‚) = sub-âŠ• (dual<= dâ‚ eâ‚ sâ‚) (dual<= dâ‚‚ eâ‚‚ sâ‚‚)
dual<= (d-âŠ•-& dâ‚ dâ‚‚) (d-âŠ•-& eâ‚ eâ‚‚) (sub-âŠ• sâ‚ sâ‚‚) = sub-& (dual<= dâ‚ eâ‚ sâ‚) (dual<= dâ‚‚ eâ‚‚ sâ‚‚)
dual<= (d-â…‹-âŠ— dâ‚ dâ‚‚) (d-â…‹-âŠ— eâ‚ eâ‚‚) (sub-â…‹ sâ‚ sâ‚‚) = sub-âŠ— (dual<= dâ‚ eâ‚ sâ‚) (dual<= dâ‚‚ eâ‚‚ sâ‚‚)
dual<= (d-âŠ—-â…‹ dâ‚ dâ‚‚) (d-âŠ—-â…‹ eâ‚ eâ‚‚) (sub-âŠ— sâ‚ sâ‚‚) = sub-â…‹ (dual<= dâ‚ eâ‚ sâ‚) (dual<= dâ‚‚ eâ‚‚ sâ‚‚)
```

## Soundness

For the results that follow, it is convenient to extend subtyping
from types to typing contexts pointwise.

```agda
infix 4 _<=âº_

data _<=âº_ : Context -> Context -> Set where
  zero : [] <=âº []
  succ : âˆ€{A B Î“ Î”} -> A <= B -> Î“ <=âº Î” -> A :: Î“ <=âº B :: Î”
```

Next we have two expected results relating subtyping (for typing
contexts) and splitting.

```agda
split<=âº : âˆ€{Î“ Î“â‚ Î“â‚‚ Î”} -> Î“ <=âº Î” -> Î“ â‰ƒ Î“â‚ + Î“â‚‚ ->
          âˆƒ[ Î”â‚ ] âˆƒ[ Î”â‚‚ ] Î” â‰ƒ Î”â‚ + Î”â‚‚ Ã— Î“â‚ <=âº Î”â‚ Ã— Î“â‚‚ <=âº Î”â‚‚
split<=âº zero split-e = [] , [] , split-e , zero , zero
split<=âº (succ sâ‚ sâ‚‚) (split-l p) with split<=âº sâ‚‚ p
... | Î”â‚ , Î”â‚‚ , p' , sâ‚ƒ , sâ‚„ = _ , _ , split-l p' , succ sâ‚ sâ‚ƒ , sâ‚„
split<=âº (succ sâ‚ sâ‚‚) (split-r p) with split<=âº sâ‚‚ p
... | _ , _ , p' , sâ‚ƒ , sâ‚„ = _ , _ , split-r p' , sâ‚ƒ , succ sâ‚ sâ‚„

split<= : âˆ€{Î“ Î“' A Î”} -> Î“ <=âº Î” -> Î“ â‰ƒ A , Î“' ->
          âˆƒ[ B ] âˆƒ[ Î”' ] Î” â‰ƒ B , Î”' Ã— A <= B Ã— Î“' <=âº Î”'
split<= s p with split<=âº s p
... | _ , _ , p' , succ sâ‚ zero , sâ‚ƒ = _ , _ , p' , sâ‚ , sâ‚ƒ
```

We can now prove the soundness of subtyping as the following
**subsumption** result. Any process that is well typed in Î“ can be
subsumed to a process that is well typed in Î” whenever Î“ is a
subtyping context of Î”. Note that, in order to prove this result, we
use the property that every process can be rewritten into an
"equivalent one" that uses no links.

```agda
subsumption : âˆ€{Î“ Î”} -> Î“ <=âº Î” -> Process Î“ -> Process Î”
subsumption s P with link-elimination P
... | _ , Plf = aux s Plf
  where
    aux : âˆ€{Î“ Î”} {P : Process Î“} -> Î“ <=âº Î” -> LinkFree P -> Process Î”
    aux s (fail p) with split<= s p
    ... | _ , _ , p' , sub-âŠ¤ , _ = fail p'
    aux s (wait p Plf) with split<= s p
    ... | .âŠ¤ , Î”' , p' , sub-âŠ¤ , sâ‚‚ = fail p'
    ... | .âŠ¥ , Î”' , p' , sub-âŠ¥ , sâ‚‚ = wait p' (aux sâ‚‚ Plf)
    aux s (case p Plf Qlf)  with split<= s p
    ... | _ , _ , p' , sub-âŠ¤ , sâ‚ƒ = fail p'
    ... | _ , _ , p' , sub-& sâ‚ sâ‚‚ , sâ‚ƒ = case p' (aux (succ sâ‚ sâ‚ƒ) Plf) (aux (succ sâ‚‚ sâ‚ƒ) Qlf)
    aux s (join p Plf)  with split<= s p
    ... | _ , _ , p' , sub-âŠ¤ , sâ‚‚ = fail p'
    ... | _ , _ , p' , sub-â…‹ sâ‚ sâ‚‚ , sâ‚ƒ = join p' (aux (succ sâ‚‚ (succ sâ‚ sâ‚ƒ)) Plf)
    aux (succ sub-âŠ¤ zero) close = fail (split-l split-e)
    aux (succ sub-1 zero) close = close
    aux s (select false p Plf)  with split<= s p
    ... | _ , _ , p' , sub-âŠ¤ , sâ‚‚ = fail p'
    ... | _ , _ , p' , sub-âŠ• sâ‚ sâ‚‚ , sâ‚ƒ = select false p' (aux (succ sâ‚‚ sâ‚ƒ) Plf)
    aux s (select true p Plf)  with split<= s p
    ... | _ , _ , p' , sub-âŠ¤ , sâ‚‚ = fail p'
    ... | _ , _ , p' , sub-âŠ• sâ‚ sâ‚‚ , sâ‚ƒ = select true p' (aux (succ sâ‚ sâ‚ƒ) Plf)
    aux s (fork p q Plf Qlf) with split<= s p
    ... | _ , _ , p' , sub-âŠ¤ , sâ‚ƒ = fail p'
    ... | _ , _ , p' , sub-âŠ— sâ‚ sâ‚‚ , sâ‚ƒ with split<=âº sâ‚ƒ q
    ... | _ , _ , q' , sâ‚„ , sâ‚… = fork p' q' (aux (succ sâ‚ sâ‚„) Plf) (aux (succ sâ‚‚ sâ‚…) Qlf)
    aux s (cut d p Plf Qlf)  with split<=âº s p
    ... | _ , _ , p' , sâ‚ , sâ‚‚ = cut d p' (aux (succ <=-refl sâ‚) Plf) (aux (succ <=-refl sâ‚‚) Qlf)
```
